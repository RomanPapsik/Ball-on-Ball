/COM,===========================================================================
/COM,GEOMETRY
/COM,===========================================================================

/PREP7

! Initial gap between balls
*SET,initial_gap,0

! Auxiliary radius for intersections
*SET,auxiliary_radius_upper,ball_radius_upper-2*contact_arc_length_upper
*SET,auxiliary_radius_lower,ball_radius_lower-2*contact_arc_length_lower


! Intersection angle (for ball with half-size ball)
*AFUN,DEG
*SET,intersection_angle_small,75.52248781407008

! Local coordinates
*AFUN,DEG
LOCAL,20,SPHE,0,+ball_radius_upper+initial_gap/2,0,-90
LOCAL,21,SPHE,0,+initial_gap/2,0,90,180
LOCAL,30,SPHE,0,-ball_radius_lower-initial_gap/2,0,90,180
LOCAL,31,SPHE,0,-initial_gap/2,0,-90

/COM,---------------------------------------------------------------------------
/COM,UPPER BALL
/COM,---------------------------------------------------------------------------

CSYS,20
K,211,ball_radius_upper,0
K,212,ball_radius_upper,crack_angle_upper
K,242,ball_radius_upper,crack_angle_upper
K,213,ball_radius_upper,crack_angle_upper*2
K,214,ball_radius_upper-contact_arc_length_upper,0
K,215,ball_radius_upper-contact_arc_length_upper,crack_angle_upper
K,216,ball_radius_upper-contact_arc_length_upper,crack_angle_upper*2
K,217,ball_radius_upper-contact_arc_length_upper*2,
K,218,ball_radius_upper-contact_arc_length_upper*2,crack_angle_upper
K,219,ball_radius_upper-contact_arc_length_upper*2,crack_angle_upper*2
CSYS,21
K,221,ball_radius_upper/2,0
K,222,ball_radius_upper/2,intersection_angle_small/2
K,223,ball_radius_upper/2,intersection_angle_small
CSYS,20
K,231,0,90
K,232,ball_radius_upper/2,90
K,233,ball_radius_upper,90

! Near areas
CSYS,20
A,211,212,215,214
*IF,simulate_cracks,EQ,true,THEN
A,242,213,216,215
*ELSE
A,212,213,216,215
*ENDIF
A,215,216,219,218
A,214,215,218,217

! Intermediate areas
CSYS,0
L,219,222
CSYS,20
L,213,223
L,217,221
CSYS,21
L,221,222
L,222,223
CSYS,20
A,213,223,222,219,216
A,217,218,219,222,221

! Distant areas
CSYS,21
BSPLIN,222,232,,,,,1,intersection_angle_small/2+180,,1,0
CSYS,20
L,223,233
L,221,231
A,223,233,232,222
A,221,222,232,231

! Concatenation for meshing
CSYS,20
LSEL,S,LOC,X,ball_radius_upper-2*contact_arc_length_upper,ball_radius_upper-2*contact_arc_length_upper
LSEL,R,LOC,Y,0,2*crack_angle_upper
LCCAT,ALL
LSEL,S,LOC,X,ball_radius_upper-contact_arc_length_upper*2,ball_radius_upper
LSEL,R,LOC,Y,2*crack_angle_upper,2*crack_angle_upper
LCCAT,ALL

! Select everything
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,LOWER BALL
/COM,---------------------------------------------------------------------------

CSYS,30
K,311,ball_radius_lower,0
K,312,ball_radius_lower,crack_angle_lower
K,342,ball_radius_lower,crack_angle_lower
K,313,ball_radius_lower,crack_angle_lower*2
K,314,ball_radius_lower-contact_arc_length_lower,0
K,315,ball_radius_lower-contact_arc_length_lower,crack_angle_lower
K,316,ball_radius_lower-contact_arc_length_lower,crack_angle_lower*2
K,317,ball_radius_lower-contact_arc_length_lower*2,
K,318,ball_radius_lower-contact_arc_length_lower*2,crack_angle_lower
K,319,ball_radius_lower-contact_arc_length_lower*2,crack_angle_lower*2
CSYS,31
K,321,ball_radius_lower/2,0
K,322,ball_radius_lower/2,intersection_angle_small/2
K,323,ball_radius_lower/2,intersection_angle_small
CSYS,30
K,331,0,90
K,332,ball_radius_lower/2,90
K,333,ball_radius_lower,90

! Near areas
CSYS,30
A,311,312,315,314
*IF,simulate_cracks,EQ,true,THEN
A,342,313,316,315
*ELSE
A,312,313,316,315
*ENDIF
A,315,316,319,318
A,314,315,318,317

! Intermediate areas
CSYS,0
L,319,322
CSYS,30
L,313,323
L,317,321
CSYS,31
L,321,322
L,322,323
CSYS,30
A,313,323,322,319,316
A,317,318,319,322,321

! Distant areas
CSYS,31
BSPLIN,322,332,,,,,1,intersection_angle_small/2+180,,1,0
CSYS,30
L,323,333
L,321,331
A,323,333,332,322
A,321,322,332,331

! Concatenation for meshing
CSYS,30
LSEL,S,LOC,X,ball_radius_lower-2*contact_arc_length_lower,ball_radius_lower-2*contact_arc_length_lower
LSEL,R,LOC,Y,0,2*crack_angle_lower
LCCAT,ALL
LSEL,S,LOC,X,ball_radius_lower-contact_arc_length_lower*2,ball_radius_lower
LSEL,R,LOC,Y,2*crack_angle_lower,2*crack_angle_lower
LCCAT,ALL

! Select everything
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,COMPONENTS
/COM,---------------------------------------------------------------------------

KSEL,S,KP,,211
KSEL,A,KP,,212
KSEL,A,KP,,242
KSEL,A,KP,,213
LSLK,S,1
CM,contact_area_upper,LINE

KSEL,S,KP,,311
KSEL,A,KP,,312
KSEL,A,KP,,342
KSEL,A,KP,,313
LSLK,S,1
CM,contact_area_lower,LINE

KSEL,S,KP,,215
KSEL,A,KP,,212
LSLK,S,1
CM,crack_face_upper_inner,LINE

KSEL,S,KP,,215
KSEL,A,KP,,242
LSLK,S,1
CM,crack_face_upper_outer,LINE

KSEL,S,KP,,315
KSEL,A,KP,,312
LSLK,S,1
CM,crack_face_lower_inner,LINE

KSEL,S,KP,,315
KSEL,A,KP,,342
LSLK,S,1
CM,crack_face_lower_outer,LINE

ASEL,S,AREA,,1,8,+1,1
CM,ball_upper,AREA

ASEL,S,AREA,,9,16,+1,1
CM,ball_lower,AREA

! Select everything
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,RIGID LINE
/COM,---------------------------------------------------------------------------

! Rigid line
CSYS,0
K,98,0,0
K,99,1.5*contact_radius,0

! Orientation decides contact normals
L,99,98
