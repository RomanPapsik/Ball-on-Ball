/COM,===========================================================================
/COM,GEOMETRY
/COM,===========================================================================

/PREP7

! Initial gap between balls
*SET,initial_gap,0*millimeter

/COM,---------------------------------------------------------------------------
/COM,AUXILIARY COORDINATE SYSTEMS
/COM,---------------------------------------------------------------------------

CSYS,0

! Lower ball
K,100,0,-ball_radius_lower-initial_gap
K,101,0,-initial_gap
K,102,ball_radius_lower,-ball_radius_lower-initial_gap

! Upper ball
K,200,0,+ball_radius_upper+initial_gap
K,201,0,+initial_gap
K,202,ball_radius_upper,+ball_radius_upper+initial_gap

! Spherical at the lower ball centre
*SET,cs_ball_lower,100
CSKP,cs_ball_lower,CYLIN,100,101,102

! Spherical at the upper ball centre
*SET,cs_ball_upper,200
CSKP,cs_ball_upper,CYLIN,200,201,202

/COM,---------------------------------------------------------------------------
/COM,LOWER BALL
/COM,---------------------------------------------------------------------------

! Centre keypoint
CSYS,cs_ball_lower
! Contact vicinity outer
K,111,ball_radius_lower,0
K,112,ball_radius_lower,crack_angle_lower
K,113,ball_radius_lower,2*crack_angle_lower
! Contact vicinity middle
K,121,ball_radius_lower-contact_arc_length_lower/2,0
K,122,ball_radius_lower-contact_arc_length_lower/2,+1*crack_angle_lower
K,123,ball_radius_lower-contact_arc_length_lower/2,+2*crack_angle_lower
! Contact vicinity inner
K,131,ball_radius_lower-contact_arc_length_lower,0
K,132,ball_radius_lower-contact_arc_length_lower,+1*crack_angle_lower
K,133,ball_radius_lower-contact_arc_length_lower,+2*crack_angle_lower

! Faraway keypoints
CSYS,0
KBETW,100,131,141,RATIO,0.5
KBETW,100,102,142,RATIO,0.5
CSYS,cs_ball_lower
KBETW,113,102,143,RATIO,DISTKP(131,141)/DISTKP(113,102)
BSPLIN,141,143,,,,,1,-90,0,1,KY(143),0
LDIV,LSNEXT(0),0.5,144,2,0
CSYS,0
L,133,144
BSPLIN,142,144,,,,,0,-1,0,KX(133)-KX(144),KY(133)-KY(144),0

! Vicinity areas
CSYS,cs_ball_lower
A,111,112,122,121
A,112,113,123,122
A,121,122,132,131
A,122,123,133,132
! Faraway areas
A,131,141,144,133,132
A,133,144,143,113,123
A,100,141,144,142
A,142,144,143,102

! Concatenation for meshing
CSYS,cs_ball_lower
KSEL,S,KP,,131 $ KSEL,A,KP,,132 $ KSEL,A,KP,,133 $ LSLK,S,1
LCCAT,ALL
KSEL,S,KP,,133 $ KSEL,A,KP,,123 $ KSEL,A,KP,,113 $ LSLK,S,1
LCCAT,ALL

! Select everything
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,UPPER BALL
/COM,---------------------------------------------------------------------------

*IF,simulate_two_balls,EQ,true,THEN

! Centre keypoint
CSYS,cs_ball_upper
! Contact vicinity outer
K,211,ball_radius_upper,0
K,212,ball_radius_upper,crack_angle_upper
K,213,ball_radius_upper,2*crack_angle_upper
! Contact vicinity middle
K,221,ball_radius_upper-contact_arc_length_upper/2,0
K,222,ball_radius_upper-contact_arc_length_upper/2,+1*crack_angle_upper
K,223,ball_radius_upper-contact_arc_length_upper/2,+2*crack_angle_upper
! Contact vicinity inner
K,231,ball_radius_upper-contact_arc_length_upper,0
K,232,ball_radius_upper-contact_arc_length_upper,+1*crack_angle_upper
K,233,ball_radius_upper-contact_arc_length_upper,+2*crack_angle_upper

! Faraway keypoints
CSYS,0
KBETW,200,231,241,RATIO,0.5
KBETW,200,202,242,RATIO,0.5
CSYS,cs_ball_upper
KBETW,213,202,243,RATIO,DISTKP(231,241)/DISTKP(213,202)
LSEL,none
BSPLIN,241,243,,,,,1,-90,0,1,KY(243),0
LDIV,LSNEXT(0),0.5,244,2,0
CSYS,0
L,233,244
BSPLIN,242,244,,,,,0,1,0,KX(233)-KX(244),KY(233)-KY(244),0

! Vicinity areas
CSYS,cs_ball_upper
A,211,212,222,221
A,212,213,223,222
A,221,222,232,231
A,222,223,233,232
! Faraway areas
A,231,241,244,233,232
A,233,244,243,213,223
A,200,241,244,242
A,242,244,243,202

! Concatenation for meshing
CSYS,cs_ball_lower
KSEL,S,KP,,231 $ KSEL,A,KP,,232 $ KSEL,A,KP,,233 $ LSLK,S,1
LCCAT,ALL
KSEL,S,KP,,233 $ KSEL,A,KP,,223 $ KSEL,A,KP,,213 $ LSLK,S,1
LCCAT,ALL

! Select everything
ALLSEL,ALL

*ENDIF

/COM,---------------------------------------------------------------------------
/COM,COMPONENTS
/COM,---------------------------------------------------------------------------

! Areas of the ball
ASEL,S,AREA,,1,8,+1,1
CM,area_ball_lower,AREA

! Lines on the contact surface
KSEL,S,KP,,111 $ KSEL,A,KP,,112 $ KSEL,A,KP,,113 $ LSLK,S,1
CM,line_contact_lower,LINE

! Repeat for the upper ball
*IF,simulate_two_balls,EQ,true,THEN

! Areas of the ball
ASEL,S,AREA,,9,16,+1,1
CM,area_ball_upper,AREA

! Lines on the contact surface
KSEL,S,KP,,211 $ KSEL,A,KP,,212 $ KSEL,A,KP,,213 $ LSLK,S,1
CM,line_contact_upper,LINE

*ENDIF

! Select everything
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,RIGID LINE
/COM,---------------------------------------------------------------------------

*IF,simulate_two_balls,EQ,false,THEN

! Rigid line
CSYS,0
K,98,0,0
K,99,1.5*contact_radius_hertz,0

! Orientation decides contact normals
L,98,99

*ENDIF