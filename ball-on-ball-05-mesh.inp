/COM,===========================================================================
/COM,MESH
/COM,===========================================================================

/PREP7

/COM,---------------------------------------------------------------------------
/COM,STRUCTURAL ELEMENTS
/COM,---------------------------------------------------------------------------

! 2D quadratic element
ET,183,PLANE183
! Axisymmetric formulation
KEYOPT,183,3,1

/COM,---------------------------------------------------------------------------
/COM,SIZING
/COM,---------------------------------------------------------------------------

! General
LSEL,ALL
LESIZE,ALL,,,20,,1

! Vicinity of contact
ASEL,S,AREA,,1,4
ASEL,A,AREA,,9,12
LSLA,S
LESIZE,ALL,,,10,,1
ALLSEL,ALL

*SET,element_size_upper,crack_arc_length_upper/10
*SET,element_size_lower,crack_arc_length_lower/10

/COM,---------------------------------------------------------------------------
/COM,MESHING
/COM,---------------------------------------------------------------------------

TYPE,183
MAT,material_ball
MSHAPE,hexahedral_mesh,2D
MSHKEY,mapped_mesh

! Upper ball
AMESH,1,4
AMESH,5,6
AMESH,7,8

! Lower ball
*IF,simulate_two_balls,EQ,true,THEN
	AMESH,9,12
	AMESH,13,14
	AMESH,15,16
*ENDIF

/COM,---------------------------------------------------------------------------
/COM,REFINEMENT
/COM,---------------------------------------------------------------------------

! First refinement
CMSEL,S,ball_upper
ESLA,S
CSYS,20
ESEL,R,CENT,X,ball_radius_upper-contact_arc_length_upper*0.5,ball_radius_upper
ESEL,R,CENT,Y,crack_angle_upper*0.4,crack_angle_upper*1.7
EREFINE,ALL,,,3,0,OFF,ON
*IF,simulate_two_balls,EQ,true,THEN
	CMSEL,S,ball_lower
	ESLA,S
	CSYS,30
	ESEL,R,CENT,X,ball_radius_lower-contact_arc_length_lower*0.5,ball_radius_lower
	ESEL,R,CENT,Y,crack_angle_upper*0.4,crack_angle_upper*1.7
	EREFINE,ALL,,,3,0,OFF,ON
*ENDIF

! Second refinement
CMSEL,S,ball_upper
ESLA,S
CSYS,20
ESEL,R,CENT,X,ball_radius_upper-contact_arc_length_upper*0.3,ball_radius_upper
ESEL,R,CENT,Y,crack_angle_upper*0.6,crack_angle_upper*1.5
EREFINE,ALL,,,3,0,OFF,ON
*IF,simulate_two_balls,EQ,true,THEN
	CMSEL,S,ball_lower
	CSYS,30
	ESEL,S,CENT,Y,crack_angle_upper*0.6,crack_angle_lower*1.5
	ESEL,R,CENT,X,ball_radius_lower-contact_arc_length_lower*0.3,ball_radius_lower
	ESLA,R
	EREFINE,ALL,,,3,0,OFF,ON
*ENDIF

! Third refinement
CMSEL,S,ball_upper
CSYS,20
ESEL,S,CENT,Y,contact_angle_upper*0.8,crack_angle_upper*1.2
ESEL,R,CENT,X,ball_radius_upper-contact_arc_length_upper*0.18,ball_radius_upper
ESLA,R
EREFINE,ALL,,,3,0,OFF,ON
*IF,simulate_two_balls,EQ,true,THEN
	CMSEL,S,ball_lower
	CSYS,30
	ESEL,S,CENT,Y,contact_angle_lower*0.8,crack_angle_lower*1.2
	ESEL,R,CENT,X,ball_radius_lower-contact_arc_length_lower*0.18,ball_radius_lower
	ESLA,R
	EREFINE,ALL,,,3,0,OFF,ON
*ENDIF

! Fourth refinement
CMSEL,S,ball_upper
CSYS,20
ESEL,S,CENT,Y,contact_angle_lower*0.97,crack_angle_lower*1.1
ESEL,R,CENT,X,ball_radius_lower-contact_arc_length_lower*0.07,ball_radius_lower
ESLA,R
EREFINE,ALL,,,3,0,OFF,ON
*IF,simulate_two_balls,EQ,true,THEN
	CMSEL,S,ball_lower
	CSYS,30
	ESEL,S,CENT,Y,contact_angle_lower*0.97,crack_angle_lower*1.1
	ESEL,R,CENT,X,ball_radius_lower-contact_arc_length_lower*0.07,ball_radius_lower
	ESLA,R
	EREFINE,ALL,,,3,0,OFF,ON
*ENDIF

! Fifth refinement
CMSEL,S,ball_upper
CSYS,20
NSEL,S,LOC,Y,contact_angle_upper,contact_angle_upper*1.05
NSEL,R,LOC,X,ball_radius_upper-contact_arc_length_upper*0.01,ball_radius_upper
NSEL,A,LOC,Y,crack_angle_upper*0.99,crack_angle_upper*1.01
NSEL,R,LOC,X,ball_radius_upper-contact_arc_length_upper*0.05,ball_radius_upper
ESLN,S,1,ALL
EREFINE,ALL,,,3,0,OFF,ON
*IF,simulate_two_balls,EQ,true,THEN
	CMSEL,S,ball_lower
	CSYS,30
	NSEL,S,LOC,Y,contact_angle_lower,contact_angle_lower*1.05
	NSEL,R,LOC,X,ball_radius_lower-contact_arc_length_lower*0.01,ball_radius_lower
	NSEL,A,LOC,Y,crack_angle_lower*0.99,crack_angle_lower*1.01
	NSEL,R,LOC,X,ball_radius_lower-contact_arc_length_lower*0.05,ball_radius_lower
	ESLN,S,1,ALL
	EREFINE,ALL,,,3,0,OFF,ON
*ENDIF

! Sixth refinement
CMSEL,S,crack_face_upper_inner
CMSEL,A,crack_face_upper_outer
NSLL,S,1
ESLN,S,0,ALL
CSYS,20
ESEL,R,CENT,X,ball_radius_upper-10*micrometer,ball_radius_upper
EREFINE,ALL,,,3,2,OFF,ON
*IF,simulate_two_balls,EQ,true,THEN
	CMSEL,S,crack_face_lower_inner
	CMSEL,A,crack_face_lower_outer
	NSLL,S,1
	ESLN,S,0,ALL
	CSYS,20
	ESEL,R,CENT,X,ball_radius_lower-10*micrometer,ball_radius_lower
	EREFINE,ALL,,,3,0,OFF,ON
*ENDIF

! Select everything
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,TOLERANCES
/COM,---------------------------------------------------------------------------

! Small element size
*SET,resized_element_size_upper,element_size_upper/(3**6)
*SET,resized_element_size_lower,element_size_lower/(3**6)

! Precision for stitching
*SET,stitching_precision_upper,resized_element_size_upper/3
*SET,stitching_precision_lower,resized_element_size_lower/3

! Tolerance for selections
SELTOL,MIN(stitching_precision_upper,stitching_precision_lower)

/COM,---------------------------------------------------------------------------
/COM,BALL CONTACT
/COM,---------------------------------------------------------------------------

! 2D Target Segment
ET,169,TARGE169
*IF,simulate_two_balls,EQ,false,THEN
	! Boundary conditions
	KEYOPT,169,2,0 ! (Automatic constrain)
*ENDIF

! 2D 3-Node Surface-to-Surface Contact 
ET,172,CONTA172
! Contact algorithm
! KEYOPT,172,2,0 ! (Augmented Lagrange)
KEYOPT,172,2,4 ! (Pure Lagrange)
! Contact detection
! KEYOPT,172,4,0 ! (At Gauss points)
KEYOPT,172,4,2 ! (normal to target)
! Automated adjustment
KEYOPT,172,5,1 ! (Auto ICONT)
! Ininital gap condideration
KEYOPT,172,9,1 ! (Excluding)

*IF,simulate_two_balls,EQ,true,THEN

	! Contact pair real set number
	*SET,pair_balls_1,11
	*SET,pair_balls_2,12

	! Select nodes on upper ball
	CMSEL,S,contact_area_upper
	NSLL,S,1
	! Target elements
	TYPE,169
	REAL,pair_balls_1
	ESURF
	! Contact elements
	TYPE,172
	REAL,pair_balls_2
	ESURF

	! Select nodes in contact
	CMSEL,S,contact_area_lower
	NSLL,S,1
	! Target elements
	TYPE,169
	REAL,pair_balls_2
	ESURF
	! Contact elements
	TYPE,172
	REAL,pair_balls_1
	ESURF

	! Select everything
	ALLSEL,ALL

	! Target circle radius
	RMODIF,pair_balls_1,1,ball_radius_upper
	RMODIF,pair_balls_2,1,ball_radius_lower
	! Initial closure (ICONT)
	RMODIF,pair_balls_1,5,contact_sagitta_lower+contact_sagitta_upper+initial_gap/2
	RMODIF,pair_balls_2,5,contact_sagitta_lower+contact_sagitta_upper+initial_gap/2
	! Pinball region (PINB)
	RMODIF,pair_balls_1,6,-(contact_sagitta_lower+contact_sagitta_upper)*1.2
	RMODIF,pair_balls_2,6,-(contact_sagitta_lower+contact_sagitta_upper)*1.2

*ELSE

	! Contact pair real set number
	*SET,pair_balls_1,11

	! Select nodes on upper ball
	CMSEL,S,contact_area_upper
	NSLL,S,1
	! Target elements
	TYPE,172
	REAL,pair_balls_1
	ESURF

	! Rigid line
	KSEL,S,KP,,98,99
	LSLK,S,1
	LESIZE,ALL,,,20,,1
	TYPE,169
	REAL,pair_balls_1
	LMESH,ALL
	LSEL,ALL

	! Target circle radius
	RMODIF,pair_balls_1,1,0
	! Initial closure (ICONT)
	RMODIF,pair_balls_1,5,contact_sagitta_upper
	! Pinball region (PINB)
	RMODIF,pair_balls_1,6,-contact_sagitta_upper*1.5

*ENDIF

/COM,---------------------------------------------------------------------------
/COM,CRACK FACES CONTACT
/COM,---------------------------------------------------------------------------

*IF,simulate_cracks,EQ,true,THEN

! 2D Target Segment
ET,168,TARGE169

! 2D 3-Node Surface-to-Surface Contact 
ET,173,CONTA172
! Contact algorithm
! KEYOPT,172,2,0 ! (Augmented Lagrange)
KEYOPT,172,2,4 ! (Pure Lagrange)
! Contact detection
! KEYOPT,172,4,0 ! (At Gauss points)
KEYOPT,172,4,2 ! (normal to target)
! Automated adjustment
KEYOPT,172,5,0 ! (No adjustment)
! Ininital gap condideration
KEYOPT,172,9,1 ! (Excluding)

*ENDIF

*IF,simulate_cracks,EQ,true,THEN

	! Contact pair real set number
	*SET,pair_upper_crack_1,21
	*SET,pair_upper_crack_2,22

	! Select nodes on inner face
	CMSEL,S,crack_face_upper_inner
	NSLL,S,1
	! Target elements
	TYPE,168
	REAL,pair_upper_crack_1
	ESURF
	! Contact elements
	TYPE,173
	REAL,pair_upper_crack_2
	ESURF

	! Select nodes on outer face
	CMSEL,S,crack_face_upper_outer
	NSLL,S,1
	! Target elements
	TYPE,168
	REAL,pair_upper_crack_2
	ESURF
	! Contact elements
	TYPE,173
	REAL,pair_upper_crack_1
	ESURF

	! Select everything
	ALLSEL,ALL

	! Target circle radius
	RMODIF,pair_upper_crack_1,1,0
	RMODIF,pair_upper_crack_2,1,0
	! Pinball region (PINB)
	RMODIF,pair_upper_crack_1,6,-micrometer
	RMODIF,pair_upper_crack_2,6,-micrometer

*ENDIF

*IF,simulate_cracks,EQ,true,AND,simulate_two_balls,EQ,true,THEN

	! Contact pair real set number
	*SET,pair_lower_crack_1,31
	*SET,pair_lower_crack_2,32

	! Select nodes on inner face
	CMSEL,S,crack_face_lower_inner
	NSLL,S,1
	! Target elements
	TYPE,168
	REAL,pair_lower_crack_1
	ESURF
	! Contact elements
	TYPE,173
	REAL,pair_lower_crack_2
	ESURF

	! Select nodes on outer face
	CMSEL,S,crack_face_lower_outer
	NSLL,S,1
	TYPE,168
	REAL,pair_lower_crack_2
	ESURF
	! Contact elements
	TYPE,173
	REAL,pair_lower_crack_1
	ESURF

	! Select everything
	ALLSEL,ALL

	! Target circle radius
	RMODIF,pair_lower_crack_1,1,0
	RMODIF,pair_lower_crack_2,1,0
	! Pinball region (PINB)
	RMODIF,pair_lower_crack_1,6,-micrometer
	RMODIF,pair_lower_crack_2,6,-micrometer

*ENDIF

/COM,---------------------------------------------------------------------------
/COM,STATISTICS
/COM,---------------------------------------------------------------------------

! Select everything
ALLSEL,ALL

! Number of nodes
*GET,nodes_count,NODE,,COUNT
! Number of elements
*GET,elements_count,ELEM,,COUNT

! Plot mesh
CSYS,0
/FOCUS,ALL,contact_radius,0
EPLOT
