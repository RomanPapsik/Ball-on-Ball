/COM,===========================================================================
/COM,MESH
/COM,===========================================================================

/PREP7

/COM,---------------------------------------------------------------------------
/COM,CONTACT ELEMENTS FOR BALLS
/COM,---------------------------------------------------------------------------

! 2D Target Segment
*SET,element_target_ball,169
ET,element_target_ball,TARGE169

*IF,simulate_cracks,EQ,false,THEN
	! Rigid line boundary conditions
	KEYOPT,element_target_ball,2,0 ! (Automatic constrain)
*ENDIF

! 2D 3-Node Surface-to-Surface Contact 
*SET,element_contact_ball,172
ET,element_contact_ball,CONTA172

! Contact algorithm
! KEYOPT,element_contact_ball,2,0 ! (Augmented Lagrange)
KEYOPT,element_contact_ball,2,4 ! (Pure Lagrange)

! Contact detection
! KEYOPT,element_contact_ball,4,0 ! (At Gauss points)
KEYOPT,element_contact_ball,4,2 ! (normal to target)

! Automated adjustment
KEYOPT,element_contact_ball,5,1 ! (Auto ICONT)

! Ininital gap condideration
KEYOPT,element_contact_ball,9,1 ! (Excluding)

/COM,---------------------------------------------------------------------------
/COM,BALL TO BALL CONTACT
/COM,---------------------------------------------------------------------------

! One ball against rigid line
*IF,simulate_two_balls,EQ,false,THEN

	! Contact pair real set number
	*SET,pair_ball_line,10

	! Rigid line target elements
	KSEL,S,KP,,98,99
	LSLK,S,1
	LESIZE,ALL,,,20,,1
	REAL,pair_ball_line
	TYPE,element_target_ball
	LMESH,ALL
	LSEL,ALL

	! Flexible ball contact elements
	CMSEL,S,line_contact_lower,LINE
	NSLL,S,1
	REAL,pair_ball_line
	TYPE,element_contact_ball
	ESURF

	! Target circle radius
	RMODIF,pair_ball_line,1,0
	! Initial closure (ICONT)
	RMODIF,pair_ball_line,5,contact_sagitta_lower
	! Pinball region (PINB)
	RMODIF,pair_ball_line,6,-contact_sagitta_lower*1.5

! Two balls against each other
*ELSEIF,simulate_two_balls,EQ,true,THEN

	! Contact pair real set number
	*SET,pair_balls_12,12
	*SET,pair_balls_21,21

	! Target elements upper ball
	CMSEL,S,line_contact_upper
	NSLL,S,1
	REAL,pair_balls_12
	TYPE,element_target_ball
	ESURF

	! Contact elements lower ball
	CMSEL,S,line_contact_lower
	NSLL,S,1
	REAL,pair_balls_12
	TYPE,element_contact_ball
	ESURF

	! Target elements lower ball
	CMSEL,S,line_contact_lower
	NSLL,S,1
	REAL,pair_balls_21
	TYPE,element_target_ball
	ESURF

	! Contact elements upper ball
	CMSEL,S,line_contact_upper
	NSLL,S,1
	REAL,pair_balls_21
	TYPE,element_contact_ball
	ESURF

	! Select everything
	ALLSEL,ALL

	! Target circle radius
	RMODIF,pair_balls_12,1,ball_radius_upper
	RMODIF,pair_balls_21,1,ball_radius_lower
	! Initial closure (ICONT)
	RMODIF,pair_balls_12,5,contact_sagitta_lower+contact_sagitta_upper+initial_gap/2
	RMODIF,pair_balls_21,5,contact_sagitta_lower+contact_sagitta_upper+initial_gap/2
	! Pinball region (PINB)
	RMODIF,pair_balls_12,6,-(contact_sagitta_lower+contact_sagitta_upper)*1.2
	RMODIF,pair_balls_21,6,-(contact_sagitta_lower+contact_sagitta_upper)*1.2

*ENDIF

/COM,---------------------------------------------------------------------------
/COM,CONTACT ELEMENTS FOR CRACKS
/COM,---------------------------------------------------------------------------

! Prevent overlap of crack faces
*IF,simulate_cracks,EQ,true,THEN

! 2D Target Segment
*SET,element_target_crack,168
ET,element_target_crack,TARGE169

! 2D 3-Node Surface-to-Surface Contact
*SET,element_contact_crack,173
ET,element_contact_crack,CONTA172

! Contact algorithm
! KEYOPT,element_contact_crack,2,0 ! (Augmented Lagrange)
KEYOPT,element_contact_crack,2,4 ! (Pure Lagrange)

! Contact detection
! KEYOPT,element_contact_crack,4,0 ! (At Gauss points)
KEYOPT,element_contact_crack,4,2 ! (normal to target)

! Automated adjustment
KEYOPT,element_contact_crack,5,0 ! (No adjustment)

! Ininital gap condideration
KEYOPT,element_contact_crack,9,1 ! (Excluding)

*ENDIF

/COM,---------------------------------------------------------------------------
/COM,CRACK FACES CONTACT
/COM,---------------------------------------------------------------------------

*IF,simulate_cracks,EQ,true,THEN

	! Contact pair real set number
	*SET,pair_upper_crack_1,31
	*SET,pair_upper_crack_2,32

	! Select nodes on inner face
	CMSEL,S,crack_face_upper_inner
	NSLL,S,1
	! Target elements
	TYPE,168
	REAL,pair_upper_crack_1
	ESURF
	! Contact elements
	TYPE,173
	REAL,pair_upper_crack_2
	ESURF

	! Select nodes on outer face
	CMSEL,S,crack_face_upper_outer
	NSLL,S,1
	! Target elements
	TYPE,168
	REAL,pair_upper_crack_2
	ESURF
	! Contact elements
	TYPE,173
	REAL,pair_upper_crack_1
	ESURF

	! Select everything
	ALLSEL,ALL

	! Target circle radius
	RMODIF,pair_upper_crack_1,1,0
	RMODIF,pair_upper_crack_2,1,0
	! Pinball region (PINB)
	RMODIF,pair_upper_crack_1,6,-micrometer
	RMODIF,pair_upper_crack_2,6,-micrometer

	*IF,simulate_two_balls,EQ,true,THEN

		! Contact pair real set number
		*SET,pair_lower_crack_1,41
		*SET,pair_lower_crack_2,42

		! Select nodes on inner face
		CMSEL,S,crack_face_lower_inner
		NSLL,S,1
		! Target elements
		TYPE,168
		REAL,pair_lower_crack_1
		ESURF
		! Contact elements
		TYPE,173
		REAL,pair_lower_crack_2
		ESURF

		! Select nodes on outer face
		CMSEL,S,crack_face_lower_outer
		NSLL,S,1
		TYPE,168
		REAL,pair_lower_crack_2
		ESURF
		! Contact elements
		TYPE,173
		REAL,pair_lower_crack_1
		ESURF

		! Select everything
		ALLSEL,ALL

		! Target circle radius
		RMODIF,pair_lower_crack_1,1,0
		RMODIF,pair_lower_crack_2,1,0
		! Pinball region (PINB)
		RMODIF,pair_lower_crack_1,6,-micrometer
		RMODIF,pair_lower_crack_2,6,-micrometer

	*ENDIF

*ENDIF
