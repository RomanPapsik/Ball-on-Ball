/COM,===========================================================================
/COM,STRESSES
/COM,===========================================================================

/COM,---------------------------------------------------------------------------
/COM,CLOSE CRACKS
/COM,---------------------------------------------------------------------------

/PREP7

*IF,simulate_cracks,EQ,true,THEN
	! Upper crack
	CMSEL,S,crack_face_upper_inner
	CMSEL,A,crack_face_upper_outer
	NSLL,S,1
	NUMMRG,NODE,stitching_precision_upper
	! Lower crack
	CMSEL,S,crack_face_lower_inner
	CMSEL,A,crack_face_lower_outer
	NSLL,S,1
	NUMMRG,NODE,stitching_precision_lower
*ENDIF

! Select everything
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,SOLVER
/COM,---------------------------------------------------------------------------

/SOLU
CSYS,1

! New analysis
ANTYPE,STATIC,NEW

! Substeps
AUTOTS,ON
NSUBST,100,1000,5

! Save all nodal data for every substep
OUTRES,ALL,ALL

! Direct sparse solver
EQSLV,SPAR
DSPOPTION,,INCORE

! Finite deformations
NLGEOM,ON

! Solve
ALLSEL,ALL
/UIS,MSGPOP,3
SOLVE
/UIS,MSGPOP,0

/POST1
RSYS,1
/DSCALE,ALL,1

/COM,---------------------------------------------------------------------------
/COM,DISPLACEMENT
/COM,---------------------------------------------------------------------------

/POST1

CSYS,20
*SET,downward_displacement_crackless,ABS(UX(loaded_node))

/COM,---------------------------------------------------------------------------
/COM,STRAIN ENERGY
/COM,---------------------------------------------------------------------------

/POST1

! Select everything
CMSEL,S,ball_upper
ESLA,S
ESEL,R,TYPE,,183
CSYS,1
RSYS,1
! Erase element table
ETABLE,ERAS
! Fill element table with strain energy
ETABLE,energ,SENE
! Sum all element table variables
SSUM
! Set sum of strain energy over all elements
*GET,energy_crackless_upper,SSUM,0,ITEM,energ

*IF,simulate_two_balls,EQ,true,THEN

	! Select everything
	CMSEL,S,ball_lower
	ESLA,S
	ESEL,R,TYPE,,183
	CSYS,1
	RSYS,1
	! Erase element table
	ETABLE,ERAS
	! Fill element table with strain energy
	ETABLE,energ,SENE
	! Sum all element table variables
	SSUM
	! Set sum of strain energy over all elements
	*GET,energy_crackless_lower,SSUM,0,ITEM,energ

	! Select everything
	ALLSEL,ALL

*ELSE
	*SET,energy_crackless_lower,energy_crackless_upper
*ENDIF

/COM,---------------------------------------------------------------------------
/COM,STRESS FIELD
/COM,---------------------------------------------------------------------------

/POST1
CSYS,20
RSYS,20

! Select surface nodes
NSEL,S,LOC,X,0,ball_radius_upper
NSEL,R,LOC,Y,0,2*contact_angle_upper
! Sort selected nodes by normal stress
NSORT,S,Y
! Get maximal and minimal normal stress
*GET,minimal_stress,SORT,,MIN
*GET,maximal_stress,SORT,,MAX
! Get numbers of nodes an maximal and minimal stress locations
*GET,minimal_stress_node,SORT,,IMIN
*GET,maximal_stress_node,SORT,,IMAX
! Select everything
NSEL,ALL
! Change range of plotted values
*SET,absolute_maximum,MAX(ABS(minimal_stress),ABS(maximal_stress))
! /CONTOUR,1,101,-absolute_maximum,,+absolute_maximum
/CONTOUR,ALL,40,-maximum_stress,,maximum_stress
! Plot normal stress
PLESOL,S,Y

/COM,---------------------------------------------------------------------------
/COM,STRESS AT SURFACE
/COM,---------------------------------------------------------------------------

/POST1
CSYS,20
RSYS,20

! Define new path
ESEL,S,TYPE,,183
NSLE,S,1
*SET,segments_count,NINT((contact_arc_length_upper)/(micrometer/10))
PATH,path_on_surface,2,10,segments_count
PPATH,1,node(ball_radius_upper,0,0)
PPATH,2,node(ball_radius_upper,contact_angle_upper*2,0)

! Map onto path
PDEF,Srad,S,X
PDEF,Sthe,S,Y
PDEF,Sphi,S,Z
PDEF,Sfir,S,1
PDEF,Ssec,S,2
PDEF,Sthi,S,3

! Plot paths
/AXLAB,X,Location [um]
/AXLAB,Y,Stress [MPa]
! /XRANGE,contact_radius*0.95,2*contact_radius*1.05
! /YRANGE,-stress_maximum*2,+stress_maximum*2
PLPATH,Srad,Sthe,Sphi

! Save current path into an array
PAGET,path_on_surface,TABLE
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,STRESS IN DEPTH
/COM,---------------------------------------------------------------------------

/POST1
CSYS,20
RSYS,20

! Define new path
ESEL,S,TYPE,,183
NSLE,S,1
*SET,path_length,ball_radius_upper*0.05
*SET,path_segments,NINT(path_length/(micrometer/10))
PATH,path_in_depth,2,10,path_segments
PPATH,1,node(ball_radius_upper,crack_angle_upper,0)
PPATH,2,node(ball_radius_upper-path_length,crack_angle_upper,0)

! Map onto path
PDEF,Srad,S,X
PDEF,Sthe,S,Y
PDEF,Sphi,S,Z
PDEF,Sfir,S,1
PDEF,Ssec,S,2
PDEF,Sthi,S,3

! Plot paths
/AXLAB,X,Location [um]
/AXLAB,Y,Stress [MPa]
! /XRANGE,0,10*micrometer
! /YRANGE,-stress_maximum,+stress_maximum
PLPATH,Srad,Sthe,Sphi

! Save current path into an array
PAGET,path_in_depth,TABLE
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,EXPORT
/COM,---------------------------------------------------------------------------

/INPUT,ball-on-ball-08-stress-export,inp
