/COM,===========================================================================
/COM,MESH
/COM,===========================================================================

/PREP7

/COM,---------------------------------------------------------------------------
/COM,STRUCTURAL ELEMENT
/COM,---------------------------------------------------------------------------

! 2D quadrilateral quadratic element
*SET,element_structural,183
ET,element_structural,PLANE183

! Axisymmetric formulation
KEYOPT,element_structural,3,1

/COM,---------------------------------------------------------------------------
/COM,SIZING
/COM,---------------------------------------------------------------------------

! General
LSEL,ALL
LESIZE,ALL,,,20,,1

! Vicinity of contact
ASEL,S,AREA,,1,4
ASEL,A,AREA,,9,12
LSLA,S
LESIZE,ALL,,,10,,1
ALLSEL,ALL

*SET,element_size_lower,crack_arc_length_lower/10
*SET,element_size_upper,crack_arc_length_upper/10

/COM,---------------------------------------------------------------------------
/COM,MESHING
/COM,---------------------------------------------------------------------------

TYPE,element_structural
MSHAPE,hexahedral_mesh,2D
MSHKEY,mapped_mesh

! Upper ball
MAT,material_lower
AMESH,1,4
AMESH,5,6
AMESH,7,8

*IF,simulate_two_balls,EQ,true,THEN

! Lower ball
MAT,material_upper
AMESH,9,12
AMESH,13,14
AMESH,15,16

*ENDIF

/COM,---------------------------------------------------------------------------
/COM,REFINEMENT
/COM,---------------------------------------------------------------------------

! First refinement
CMSEL,S,area_ball_lower
ESLA,S
CSYS,cs_ball_lower
ESEL,R,CENT,X,ball_radius_lower-contact_arc_length_lower*0.5,ball_radius_lower
ESEL,R,CENT,Y,crack_angle_lower*0.4,crack_angle_lower*1.7
EREFINE,ALL,,,3,0,OFF,ON

*IF,simulate_two_balls,EQ,true,THEN

CMSEL,S,area_ball_upper
ESLA,S
CSYS,cs_ball_upper
ESEL,R,CENT,X,ball_radius_upper-contact_arc_length_upper*0.5,ball_radius_upper
ESEL,R,CENT,Y,crack_angle_upper*0.4,crack_angle_upper*1.7
EREFINE,ALL,,,3,0,OFF,ON

*ENDIF

! Second refinement
CMSEL,S,area_ball_lower
ESLA,S
CSYS,cs_ball_lower
ESEL,R,CENT,X,ball_radius_lower-contact_arc_length_lower*0.3,ball_radius_lower
ESEL,R,CENT,Y,crack_angle_lower*0.6,crack_angle_lower*1.5
EREFINE,ALL,,,3,0,OFF,ON

*IF,simulate_two_balls,EQ,true,THEN

CMSEL,S,area_ball_upper
CSYS,cs_ball_upper
ESEL,S,CENT,Y,crack_angle_upper*0.6,crack_angle_upper*1.5
ESEL,R,CENT,X,ball_radius_upper-contact_arc_length_upper*0.3,ball_radius_upper
ESLA,R
EREFINE,ALL,,,3,0,OFF,ON

*ENDIF

! Third refinement
CMSEL,S,area_ball_lower
CSYS,cs_ball_lower
ESEL,S,CENT,Y,contact_angle_lower*0.8,crack_angle_lower*1.2
ESEL,R,CENT,X,ball_radius_lower-contact_arc_length_lower*0.18,ball_radius_lower
ESLA,R
EREFINE,ALL,,,3,0,OFF,ON

*IF,simulate_two_balls,EQ,true,THEN

CMSEL,S,area_ball_upper
CSYS,cs_ball_upper
ESEL,S,CENT,Y,contact_angle_upper*0.8,crack_angle_upper*1.2
ESEL,R,CENT,X,ball_radius_upper-contact_arc_length_upper*0.18,ball_radius_upper
ESLA,R
EREFINE,ALL,,,3,0,OFF,ON

*ENDIF

! Fourth refinement
CMSEL,S,area_ball_lower
CSYS,cs_ball_lower
ESEL,S,CENT,Y,contact_angle_lower*0.97,crack_angle_lower*1.1
ESEL,R,CENT,X,ball_radius_lower-contact_arc_length_lower*0.07,ball_radius_lower
ESLA,R
EREFINE,ALL,,,3,0,OFF,ON

*IF,simulate_two_balls,EQ,true,THEN

CMSEL,S,ball_upper
CSYS,cs_ball_upper
ESEL,S,CENT,Y,contact_angle_upper*0.97,crack_angle_upper*1.1
ESEL,R,CENT,X,ball_radius_upper-contact_arc_length_upper*0.07,ball_radius_upper
ESLA,R
EREFINE,ALL,,,3,0,OFF,ON

*ENDIF

! Fifth refinement
CMSEL,S,area_ball_lower
CSYS,cs_ball_lower
NSEL,S,LOC,Y,contact_angle_lower,contact_angle_lower*1.05
NSEL,R,LOC,X,ball_radius_lower-contact_arc_length_lower*0.01,ball_radius_lower
NSEL,A,LOC,Y,crack_angle_lower*0.99,crack_angle_lower*1.01
NSEL,R,LOC,X,ball_radius_lower-contact_arc_length_lower*0.05,ball_radius_lower
ESLN,S,1,ALL
EREFINE,ALL,,,3,0,OFF,ON

*IF,simulate_two_balls,EQ,true,THEN

CMSEL,S,ball_upper
CSYS,cs_ball_upper
NSEL,S,LOC,Y,contact_angle_upper,contact_angle_upper*1.05
NSEL,R,LOC,X,ball_radius_upper-contact_arc_length_upper*0.01,ball_radius_upper
NSEL,A,LOC,Y,crack_angle_upper*0.99,crack_angle_upper*1.01
NSEL,R,LOC,X,ball_radius_upper-contact_arc_length_upper*0.05,ball_radius_upper
ESLN,S,1,ALL
EREFINE,ALL,,,3,0,OFF,ON

*ENDIF

! Sixth refinement
CMSEL,S,line_crack_lower_inner
CMSEL,A,line_crack_lower_outer
NSLL,S,1
ESLN,S,0,ALL
CSYS,cs_ball_lower
ESEL,R,CENT,X,ball_radius_lower-10*micrometer,ball_radius_lower
EREFINE,ALL,,,3,2,OFF,ON

*IF,simulate_two_balls,EQ,true,THEN

CMSEL,S,line_crack_upper_inner
CMSEL,A,line_crack_upper_outer
NSLL,S,1
ESLN,S,0,ALL
CSYS,cs_ball_upper
ESEL,R,CENT,X,ball_radius_upper-10*micrometer,ball_radius_upper
EREFINE,ALL,,,3,2,OFF,ON

*ENDIF

! Select everything
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,TOLERANCES
/COM,---------------------------------------------------------------------------

! Small element size
*SET,resized_element_size_lower,element_size_lower/(3**6)
*SET,resized_element_size_upper,element_size_upper/(3**6)

! Precision for stitching
*SET,stitching_precision_lower,resized_element_size_lower/3
*SET,stitching_precision_upper,resized_element_size_upper/3

! Tolerance for selections
SELTOL,MIN(stitching_precision_lower,stitching_precision_upper)

/COM,---------------------------------------------------------------------------
/COM,STATISTICS
/COM,---------------------------------------------------------------------------

! Select everything
ALLSEL,ALL

! Number of nodes
*GET,nodes_count,NODE,,COUNT
! Number of elements
*GET,elements_count,ELEM,,COUNT

! Plot mesh
CSYS,0
/FOCUS,ALL,contact_radius_hertz,0
EPLOT
