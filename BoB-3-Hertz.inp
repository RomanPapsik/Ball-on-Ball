/COM,---------------------------------------------------------------------------
/COM,EFFECTIVE STIFFNESS
/COM,---------------------------------------------------------------------------

! Compliance
*SET,effective_compliance,(1-poisson_ratio_lower**2)/young_modulus_lower+(1-poisson_ratio_upper**2)/young_modulus_upper

! Stiffness
*SET,effective_stiffness,1/effective_compliance

/COM,---------------------------------------------------------------------------
/COM,EFFECTIVE CURVATURE
/COM,---------------------------------------------------------------------------

! Effective curvature
*SET,effective_curvature,1/ball_radius_lower+1/ball_radius_upper

! Effective radiusVenus from Willendorf
*SET,effective_radius,1/effective_curvature

/COM,---------------------------------------------------------------------------
/COM,RADII AND ANGLES
/COM,---------------------------------------------------------------------------

! Contact radius
*SET,contact_radius_hertz,((3*force*effective_radius)/(4*effective_stiffness))**(1/3)

! Contact area
*SET,contact_area_hertz,PI*(contact_radius_hertz**2)

/COM,---------------------------------------------------------------------------
/COM,PRESSURES 
/COM,---------------------------------------------------------------------------

! Contact half angle from axis
*AFUN,DEG
*SET,contact_angle_lower,ASIN(contact_radius_hertz/ball_radius_lower)
*SET,contact_angle_upper,ASIN(contact_radius_hertz/ball_radius_upper)

! Contact arc length
*SET,contact_arc_length_lower,ball_radius_lower*contact_angle_lower*PI/180
*SET,contact_arc_length_upper,ball_radius_upper*contact_angle_upper*PI/180

! Contact sagitta (height from symmetry line)
*AFUN,DEG
*SET,contact_sagitta_lower,ball_radius_lower*(1-COS(contact_angle_lower))
*SET,contact_sagitta_upper,ball_radius_upper*(1-COS(contact_angle_upper))

/COM,---------------------------------------------------------------------------
/COM,PRESSURES AND STRESSES
/COM,---------------------------------------------------------------------------

! Mean contact pressure
*SET,mean_pressure_hertz,force/contact_area_hertz

! Maximum contact pressure
*SET,maximum_pressure_hertz,1.5*mean_pressure_hertz

! Maximum radial stress
*SET,maximum_stress_hertz_lower,(0.5-poisson_ratio_lower)*mean_pressure_hertz
*SET,maximum_stress_hertz_upper,(0.5-poisson_ratio_upper)*mean_pressure_hertz

/COM,---------------------------------------------------------------------------
/COM,CRACK
/COM,---------------------------------------------------------------------------

! Crack in lower ball
*IF,crack_radius_lower,LE,0,THEN
	*SET,crack_radius_lower,contact_radius_hertz*crack_multiplier_lower
*ELSE
	*SET,crack_multiplier_lower,crack_radius_lower/contact_radius_hertz
*ENDIF

! Crack in upper ball
*IF,crack_radius_upper,LE,0,THEN
	*SET,crack_radius_upper,contact_radius_hertz*crack_multiplier_upper
*ELSE
	*SET,crack_multiplier_upper,crack_radius_upper/contact_radius_hertz
*ENDIF

! Crack half angle from axis
*AFUN,DEG
*SET,crack_angle_lower,ASIN(crack_radius_lower/ball_radius_lower)
*SET,crack_angle_upper,ASIN(crack_radius_upper/ball_radius_upper)

! Crack arc length
*SET,crack_arc_length_lower,ball_radius_lower*crack_angle_lower*PI/180
*SET,crack_arc_length_upper,ball_radius_upper*crack_angle_upper*PI/180

! Crack sagitta (height from symmetry line)
*AFUN,DEG
*SET,crack_sagitta_lower,ball_radius_lower*(1-COS(crack_angle_lower))
*SET,crack_sagitta_upper,ball_radius_upper*(1-COS(crack_angle_upper))
