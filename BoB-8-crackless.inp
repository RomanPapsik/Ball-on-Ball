/COM,===========================================================================
/COM,STRESSES
/COM,===========================================================================

/COM,---------------------------------------------------------------------------
/COM,CLOSE CRACKS
/COM,---------------------------------------------------------------------------

/PREP7

*IF,simulate_cracks,EQ,true,THEN
	! Upper crack
	CMSEL,S,crack_face_upper_inner
	CMSEL,A,crack_face_upper_outer
	NSLL,S,1
	NUMMRG,NODE,stitching_precision_upper
	! Lower crack
	CMSEL,S,crack_face_lower_inner
	CMSEL,A,crack_face_lower_outer
	NSLL,S,1
	NUMMRG,NODE,stitching_precision_lower
*ENDIF

! Select everything
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,SOLUTION
/COM,---------------------------------------------------------------------------

/SOLU

! New analysis
ANTYPE,STATIC,NEW

! Substeps
AUTOTS,ON
NSUBST,100,1000,5

! Save all nodal data for every substep
OUTRES,ALL,ALL

! Direct sparse solver
EQSLV,SPAR
DSPOPTION,,INCORE

! Finite deformations
*IF,large_deformations,EQ,true,THEN
	NLGEOM,ON
*ELSE
	NLGEOM,OFF
*ENDIF

! Select everything
ALLSEL,ALL

! Solve
/UIS,MSGPOP,3
SOLVE
/UIS,MSGPOP,0

/COM,---------------------------------------------------------------------------
/COM,DISPLACEMENT
/COM,---------------------------------------------------------------------------

/POST1

! Displacement towards the contact point
RSYS,cs_ball_lower
*GET,displacement_crackless_lower,NODE,loaded_node_lower,U,X

*IF,simulate_two_balls,EQ,true,THEN
	RSYS,cs_ball_upper
	*GET,displacement_crackless_upper,NODE,loaded_node_upper,U,X
*ENDIF

/COM,---------------------------------------------------------------------------
/COM,STRAIN ENERGY
/COM,---------------------------------------------------------------------------

/POST1

! Select elements of the ball
CMSEL,S,area_ball_lower,AREA
ESLA,S $ ESEL,R,TYPE,,element_structural
! Erase element table and fill it with the strain energy
ETABLE,ERAS $ ETABLE,energy,SENE
! Sum all element table variables
SSUM
! Set sum of strain energy over all elements
*GET,energy_crackless_lower,SSUM,0,ITEM,energy

*IF,simulate_two_balls,EQ,true,THEN

	! Select elements of the ball
	CMSEL,S,area_ball_upper,AREA
	ESLA,S $ ESEL,R,TYPE,,element_structural
	! Erase element table and fill it with the strain energy
	ETABLE,ERAS $ ETABLE,energy,SENE
	! Sum all element table variables
	SSUM
	! Set sum of strain energy over all elements
	*GET,energy_crackless_upper,SSUM,0,ITEM,energy
*ELSE
	*SET,energy_crackless_lower,0
*ENDIF

*SET,energy_crackless_total,energy_crackless_upper+energy_crackless_lower

/COM,---------------------------------------------------------------------------
/COM,STRESS FIELD
/COM,---------------------------------------------------------------------------

/POST1

! Select surface nodes
CSYS,cs_ball_lower
NSEL,S,LOC,X,0,ball_radius_upper
NSEL,R,LOC,Y,0,2*contact_angle_upper
! Sort selected nodes by tangential stress
RSYS,cs_ball_lower
NSORT,S,Y
! Get maximal and minimal tangential stress
*GET,minimal_stress,SORT,,MIN
*GET,maximal_stress,SORT,,MAX
! Get numbers of nodes an maximal and minimal stress locations
*GET,minimal_stress_node,SORT,,IMIN
*GET,maximal_stress_node,SORT,,IMAX
! Select everything
NSEL,ALL
! Change range of plotted values
*SET,absolute_maximum,MAX(ABS(minimal_stress),ABS(maximal_stress))
/CONTOUR,ALL,10,-absolute_maximum,,absolute_maximum
! Plot stress field
PLESOL,S,Y

/COM,---------------------------------------------------------------------------
/COM,PATH ON SURFACE
/COM,---------------------------------------------------------------------------

/POST1

! Select structural elements of the ball
ESEL,S,TYPE,,element_structural $ NSLE,S,1
! Calculate path division
*SET,path_division,NINT((contact_arc_length_lower)/(micrometer/4))
! Initialize new path
PATH,path_on_surface,2,10,path_division
! Define path
CSYS,cs_ball_lower
PPATH,1,node(ball_radius_lower,0,0),,,,cs_ball_lower
PPATH,2,node(ball_radius_lower,contact_angle_lower*4,0),,,,cs_ball_lower

! Map onto path
RSYS,cs_ball_lower
PDEF,Snorm,S,X
PDEF,Stang,S,Y
PDEF,Shoop,S,Z
PDEF,S1,S,1
PDEF,S2,S,2
PDEF,S3,S,3

! Plot paths
/TITLE,Path on the lower ball surface
/AXLAB,X,Path position [um]
/AXLAB,Y,Stress components [MPa]
! /XRANGE,contact_radius*0.95,2*contact_radius*1.05
! /YRANGE,-stress_maximum*2,+stress_maximum*2
PLPATH,Stang,Snorm,Shoop
PLGEOM,Stang,Snorm,Shoop
! Save current path into an array
PAGET,path_on_surface,TABLE
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,STRESS PATH IN DEPTH
/COM,---------------------------------------------------------------------------

*IF,simulate_cracks,EQ,true,THEN

/POST1
CSYS,cs_ball_lower
RSYS,cs_ball_lower

! Define new path
ESEL,S,TYPE,,element_structural
NSLE,S,1
*SET,path_length,ball_radius_upper*0.05
*SET,path_segments,NINT(path_length/(micrometer/10))
PATH,path_in_depth,2,10,path_segments
PPATH,1,node(ball_radius_upper,crack_angle_upper,0)
PPATH,2,node(ball_radius_upper-path_length,crack_angle_upper,0)

! Map onto path
PDEF,Srad,S,X
PDEF,Sthe,S,Y
PDEF,Sphi,S,Z
PDEF,Sfir,S,1
PDEF,Ssec,S,2
PDEF,Sthi,S,3

! Plot paths
/AXLAB,X,Location [um]
/AXLAB,Y,Stress [MPa]
! /XRANGE,0,10*micrometer
! /YRANGE,-stress_maximum,+stress_maximum
PLPATH,Srad,Sthe,Sphi

! Save current path into an array
PAGET,path_in_depth,TABLE
ALLSEL,ALL

*ENDIF
